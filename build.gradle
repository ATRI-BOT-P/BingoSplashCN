buildscript {
    repositories {
        maven { url "https://maven.minecraftforge.net" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1.3"
    }
}

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: "net.minecraftforge.gradle.forge"

version = "1.1.9"
group = "cn.bingosplash"
archivesBaseName = "BingoSplashCN"
sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    mavenCentral()
}

dependencies {
    shade 'javax.websocket:javax.websocket-api:1.1'
    shade 'org.glassfish.tyrus:tyrus-server:1.13'
    implementation 'com.google.code.gson:gson:2.8.9'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    classifier = ''
}

reobf {
    shadowJar {}
}
